#+vet explicit-allocators
#+build windows
#+private file

package karl2d

@(private="package")
PLATFORM_WINDOWS :: Platform_Interface {
	state_size = windows_state_size,
	init = windows_init,
	shutdown = windows_shutdown,
	get_window_render_glue = windows_get_window_render_glue,
	get_events = windows_get_events,
	set_window_title = windows_set_window_title,
	get_clipboard_text = windows_get_clipboard_text,
	set_clipboard_text = windows_set_clipboard_text,
	get_screen_width = windows_get_screen_width,
	get_screen_height = windows_get_screen_height,
	set_window_position = windows_set_window_position,
	get_window_position = windows_get_window_position,
	set_screen_size = windows_set_screen_size,
	get_window_scale = windows_get_window_scale,
	set_window_mode = windows_set_window_mode,

	set_cursor_hidden = windows_set_cursor_hidden,
	is_cursor_hidden = windows_is_cursor_hidden,
	set_mouse_locked = windows_set_mouse_locked,
	is_mouse_locked = windows_is_mouse_locked,
	create_custom_cursor = windows_create_custom_cursor,
	set_cursor = windows_set_cursor,
	destroy_custom_cursor = windows_destroy_custom_cursor,

	is_gamepad_active = windows_is_gamepad_active,
	get_gamepad_axis = windows_get_gamepad_axis,
	set_gamepad_vibration = windows_set_gamepad_vibration,

	open_url = windows_open_url,

	set_internal_state = windows_set_internal_state,
}

import win32 "core:sys/windows"
import "core:unicode/utf16"
import "core:slice"
import "base:runtime"
import hm "core:container/handle_map"
@require import "log"

windows_state_size :: proc() -> int {
	return size_of(Windows_State)
}

windows_init :: proc(
	platform_state: rawptr,
	screen_width: int,
	screen_height: int,
	window_title: string,
	options: Init_Options,
	allocator: runtime.Allocator,
) {
	assert(platform_state != nil)
	s = (^Windows_State)(platform_state)
	s.allocator = allocator
	s.events = make([dynamic]Event, allocator = allocator)
	s.custom_context = context
	hm.dynamic_init(&s.custom_cursors, allocator)

	win32.SetProcessDpiAwarenessContext(win32.DPI_AWARENESS_CONTEXT_PER_MONITOR_AWARE_V2)
	win32.SetProcessDPIAware()
	CLASS_NAME :: "karl2d"
	instance := win32.HINSTANCE(win32.GetModuleHandleW(nil))

	cls := win32.WNDCLASSW {
		style = win32.CS_OWNDC,
		lpfnWndProc = _windows_window_proc,
		lpszClassName = CLASS_NAME,
		hInstance = instance,
		hCursor = win32.LoadCursorA(nil, win32.IDC_ARROW),
	}

	win32.RegisterClassW(&cls)

	dpix, dpiy: win32.UINT
	win32.GetDpiForMonitor(win32.MonitorFromWindow(nil, .MONITOR_DEFAULTTOPRIMARY), {}, &dpix, &dpiy)
	s.window_scale = f32(dpix)/96.0

	if options.disable_auto_scale_hint {
		s.screen_width = screen_width
		s.screen_height = screen_height
	} else {
		s.screen_width = int(f32(screen_width) * s.window_scale)
		s.screen_height = int(f32(screen_height) * s.window_scale)
	}

	// Since this is the size of the screen we adjust it to become the size of the window. This is
	// done using `AdjustWindowRectExForDpi`. It adds the space needed for the window borders etc.
	initial_rect := win32.RECT {
		0,
		0,
		i32(s.screen_width),
		i32(s.screen_height),
	}

	win32.AdjustWindowRectExForDpi(&initial_rect, windows_get_style(options.window_mode), false, {}, dpix)

	// We create a window with default position and size. We set the correct size in
	// `windows_set_window_mode`.
	s.hwnd = win32.CreateWindowW(
		CLASS_NAME,
		win32.utf8_to_wstring(window_title, frame_allocator),
		win32.WS_VISIBLE,
		win32.CW_USEDEFAULT, win32.CW_USEDEFAULT,
		i32(initial_rect.right - initial_rect.left),
		i32(initial_rect.bottom - initial_rect.top),
		nil, nil, instance, nil,
	)

	assert(s.hwnd != nil, "Failed creating window")

	windows_set_window_mode(options.window_mode)
	
	win32.XInputEnable(true)

	when RENDER_BACKEND_NAME == "d3d11" {
		s.window_render_glue = {
			state = (^Window_Render_Glue_State)(s.hwnd),
		}
	} else when RENDER_BACKEND_NAME == "gl" {
		s.window_render_glue = make_windows_gl_glue(s.hwnd, s.allocator)
	}  else when RENDER_BACKEND_NAME == "nil" {
		s.window_render_glue = {}
	} else {
		#panic("Unsupported combo of Windows platform and render backend '" + RENDER_BACKEND_NAME + "'")
	}
}

windows_shutdown :: proc() {
	for it := hm.dynamic_iterator_make(&s.custom_cursors); cd, _ in hm.dynamic_iterate(&it) {
		win32.DestroyCursor(cd.hcursor)
	}
	hm.dynamic_destroy(&s.custom_cursors)

	win32.DestroyWindow(s.hwnd)
	delete(s.events)
}

windows_get_window_render_glue :: proc() -> Window_Render_Glue {
	return s.window_render_glue
}

windows_get_events :: proc(events: ^[dynamic]Event) {
	msg: win32.MSG

	// This loop will call `_windows_window_proc` which will add more things to `frame_events`.
	for win32.PeekMessageW(&msg, nil, 0, 0, win32.PM_REMOVE) {
		win32.TranslateMessage(&msg)
		win32.DispatchMessageW(&msg)
	}

	// While minimized, the DWM stops compositing the window, so IDXGISwapChain::Present no longer
	// waits for vblank. Without this, the game loop's only throttling (vsync inside Present) goes
	// away and it spins as fast as the CPU/GPU allow, pegging a core at 100%. Sleeping here caps us
	// to roughly 100 checks per second instead, while still waking up promptly when the window is
	// restored.
	if s.minimized {
		win32.Sleep(10)
	}

	// 4 is the limit set by microsoft, not by us. So I'm not using MAX_GAMEPADS here.
	for gamepad in 0..<4 {
		gp_event: win32.XINPUT_KEYSTROKE

		for win32.XInputGetKeystroke(win32.XUSER(gamepad), 0, &gp_event) == .SUCCESS {
			button: Maybe(Gamepad_Button)

			if .REPEAT in gp_event.Flags {
				continue
			}

			#partial switch gp_event.VirtualKey {
			case .DPAD_UP:    button = .Left_Face_Up
			case .DPAD_DOWN:  button = .Left_Face_Down
			case .DPAD_LEFT:  button = .Left_Face_Left
			case .DPAD_RIGHT: button = .Left_Face_Right

			case .Y: button = .Right_Face_Up
			case .A: button = .Right_Face_Down
			case .X: button = .Right_Face_Left
			case .B: button = .Right_Face_Right

			case .LSHOULDER: button = .Left_Shoulder
			case .RSHOULDER: button = .Right_Shoulder

			case .BACK: button = .Middle_Face_Left
			
			// Not sure you can get the "middle button" with XInput (the one that goes to dashboard)

			case .START: button = .Middle_Face_Right

			case .LTHUMB_PRESS: button = .Left_Stick_Press
			case .RTHUMB_PRESS: button = .Right_Stick_Press
			}

			b := button.? or_continue
			evt: Event

			if .KEYDOWN in gp_event.Flags {
				evt = Event_Gamepad_Button_Went_Down {
					gamepad = gamepad,
					button = b,
				}
			} else if .KEYUP in gp_event.Flags {
				evt = Event_Gamepad_Button_Went_Up {
					gamepad = gamepad,
					button = b,
				}
			}

			if evt != nil {
				append(&s.events, evt)
			}
		}

		// Triggers are handled separately because RTRIGGER and LTRIGGER don't get key down events
		// while held at same time.
		gp_state: win32.XINPUT_STATE
		if win32.XInputGetState(win32.XUSER(gamepad), &gp_state) == .SUCCESS {
			THRESHOLD :: win32.BYTE(win32.XINPUT_GAMEPAD_TRIGGER_THRESHOLD)

			cur_lt := gp_state.Gamepad.bLeftTrigger
			cur_rt := gp_state.Gamepad.bRightTrigger

			prev := &s.previous_gamepad_triggers[gamepad]
			prev_lt := prev[0]
			prev_rt := prev[1]

			if cur_lt >= THRESHOLD && prev_lt < THRESHOLD {
				append(&s.events, Event_Gamepad_Button_Went_Down {
					gamepad = gamepad,
					button = .Left_Trigger,
				})
			} else if cur_lt < THRESHOLD && prev_lt >= THRESHOLD {
				append(&s.events, Event_Gamepad_Button_Went_Up {
					gamepad = gamepad,
					button = .Left_Trigger,
				})
			}

			if cur_rt >= THRESHOLD && prev_rt < THRESHOLD {
				append(&s.events, Event_Gamepad_Button_Went_Down {
					gamepad = gamepad,
					button = .Right_Trigger,
				})
			} else if cur_rt < THRESHOLD && prev_rt >= THRESHOLD {
				append(&s.events, Event_Gamepad_Button_Went_Up {
					gamepad = gamepad,
					button = .Right_Trigger,
				})
			}

			prev[0] = cur_lt
			prev[1] = cur_rt
		}
	}

	append(events, ..s.events[:])
	runtime.clear(&s.events)
}

windows_get_screen_width :: proc() -> int {
	return s.screen_width
}

windows_get_screen_height :: proc() -> int {
	return s.screen_height
}

windows_set_window_title :: proc(title: string) {
	win32.SetWindowTextW(s.hwnd, win32.utf8_to_wstring(title, frame_allocator))
}

windows_get_clipboard_text :: proc(allocator: runtime.Allocator) -> (string, bool) {
	if !win32.IsClipboardFormatAvailable(win32.CF_UNICODETEXT) {
		return "", false
	}

	if !win32.OpenClipboard(s.hwnd) {
		return "", false
	}

	handle := win32.GetClipboardData(win32.CF_UNICODETEXT)
	if handle == nil {
		win32.CloseClipboard()
		return "", false
	}

	data := win32.GlobalLock(win32.HGLOBAL(handle))
	if data == nil {
		win32.CloseClipboard()
		return "", false
	}

	data_size := win32.GlobalSize(win32.LPVOID(handle))
	data_utf16 := slice.from_ptr((^u16)(data), int(data_size / size_of(u16)))
	text, text_err := win32.utf16_to_utf8(data_utf16, allocator)
	win32.GlobalUnlock(win32.HGLOBAL(handle))
	win32.CloseClipboard()

	if text_err != nil {
		return "", false
	}

	return text, true
}

windows_set_clipboard_text :: proc(text: string) -> bool {
	text_utf16 := win32.utf8_to_wstring(text, frame_allocator)
	if text_utf16 == nil {
		return false
	}

	if !win32.OpenClipboard(s.hwnd) {
		return false
	}

	if !win32.EmptyClipboard() {
		win32.CloseClipboard()
		return false
	}

	bytes := win32.SIZE_T((len(text_utf16) + 1) * size_of(u16))
	handle := win32.HGLOBAL(win32.GlobalAlloc(win32.GMEM_MOVEABLE, bytes))
	if handle == nil {
		win32.CloseClipboard()
		return false
	}

	data := win32.GlobalLock(handle)
	if data == nil {
		win32.GlobalFree(win32.LPVOID(handle))
		win32.CloseClipboard()
		return false
	}

	text_utf16_slice := slice.from_ptr((^u16)(text_utf16), len(text_utf16) + 1)
	runtime.mem_copy_non_overlapping(data, raw_data(text_utf16_slice), int(bytes))
	win32.GlobalUnlock(handle)

	if win32.SetClipboardData(win32.CF_UNICODETEXT, win32.HANDLE(handle)) == nil {
		win32.GlobalFree(win32.LPVOID(handle))
		win32.CloseClipboard()
		return false
	}

	win32.CloseClipboard()
	return true
}

// Because positions can be offset in Windows: There is an "inivisble border" on Windows. This makes
// windows end up at slight offset positions. For example, if you set a window to be at (0, 0), then
// it won't be at (0, 0) unless you add this offset.
windows_get_window_offset :: proc() -> (x, y: i32) {
	real_r: win32.RECT
	win32.DwmGetWindowAttribute(s.hwnd, u32(win32.DWMWINDOWATTRIBUTE.DWMWA_EXTENDED_FRAME_BOUNDS), &real_r, size_of(win32.RECT))

	r: win32.RECT
	win32.GetWindowRect(s.hwnd, &r)

	return real_r.left - r.left, real_r.top - r.top
}

windows_set_window_position :: proc(x: int, y: int) {
	offx, offy := windows_get_window_offset()

	win32.SetWindowPos(
		s.hwnd,
		{},
		i32(x) - offx,
		i32(y) - offy,
		0,
		0,
		win32.SWP_NOACTIVATE | win32.SWP_NOZORDER | win32.SWP_NOSIZE,
	)
}

windows_get_window_position :: proc() -> Vec2 {
	r: win32.RECT
	win32.DwmGetWindowAttribute(
		s.hwnd,
		u32(win32.DWMWINDOWATTRIBUTE.DWMWA_EXTENDED_FRAME_BOUNDS),
		&r,
		size_of(win32.RECT),
	)
	return {f32(r.left), f32(r.top)}
}

windows_get_style :: proc(window_mode: Window_Mode) -> win32.DWORD {
	style: win32.DWORD

	switch window_mode {
	case .Windowed:
		style = win32.WS_OVERLAPPED |
	            win32.WS_CAPTION |
	            win32.WS_SYSMENU |
	            win32.WS_MINIMIZEBOX |
	            win32.WS_VISIBLE

	case .Windowed_Resizable:
		style = win32.WS_OVERLAPPED |
	            win32.WS_CAPTION |
	            win32.WS_SYSMENU |
	            win32.WS_MINIMIZEBOX |
	            win32.WS_VISIBLE |
	            win32.WS_THICKFRAME |
	            win32.WS_MAXIMIZEBOX

	case .Borderless_Fullscreen:
		style = win32.WS_VISIBLE
	}

	return style
}

windows_set_screen_size :: proc(w, h: int) {
	s.screen_width = w
	s.screen_height = h

	r: win32.RECT
	r.left = 0
	r.top = 0
	r.right = i32(w)
	r.bottom = i32(h)

	win32.AdjustWindowRectExForDpi(&r, windows_get_style(s.window_mode), false, 0, win32.GetDpiForWindow(s.hwnd))
	win32.SetWindowPos(
		s.hwnd,
		{},
		0,
		0,
		r.right - r.left,
		r.bottom - r.top,
		win32.SWP_NOACTIVATE | win32.SWP_NOZORDER | win32.SWP_NOMOVE,
	)
}

windows_get_window_scale :: proc() -> f32 {
	return s.window_scale
}

windows_is_gamepad_active :: proc(gamepad: int) -> bool {
	if gamepad < 0 || gamepad >= MAX_GAMEPADS {
		return false
	}

	gp_state: win32.XINPUT_STATE
	return win32.XInputGetState(win32.XUSER(gamepad), &gp_state) == .SUCCESS
}

windows_get_gamepad_axis :: proc(gamepad: int, axis: Gamepad_Axis) -> f32 {
	if gamepad < 0 || gamepad >= MAX_GAMEPADS {
		return 0
	}

	gp_state: win32.XINPUT_STATE
	if win32.XInputGetState(win32.XUSER(gamepad), &gp_state) == .SUCCESS {
		gp := gp_state.Gamepad

		// Numbers from https://learn.microsoft.com/en-us/windows/win32/api/xinput/ns-xinput-xinput_gamepad
		STICK_MAX   :: 32767
		TRIGGER_MAX :: 255

		switch axis {
		case .None: return 0
		case .Left_Stick_X: return f32(gp.sThumbLX) / STICK_MAX
		case .Left_Stick_Y: return -f32(gp.sThumbLY) / STICK_MAX
		case .Right_Stick_X: return f32(gp.sThumbRX) / STICK_MAX
		case .Right_Stick_Y: return -f32(gp.sThumbRY) / STICK_MAX
		case .Left_Trigger: return f32(gp.bLeftTrigger) / TRIGGER_MAX
		case .Right_Trigger: return f32(gp.bRightTrigger) / TRIGGER_MAX
		}
	}

	return 0
}

windows_open_url :: proc(url: string) -> bool {
	cmd := win32.utf8_to_wstring(url, frame_allocator)
	res := win32.ShellExecuteW(s.hwnd, "open", cmd, nil, nil, win32.SW_NORMAL)

	// https://learn.microsoft.com/en-us/windows/win32/api/shellapi/nf-shellapi-shellexecutew#return-value:
	// If the function succeeds, it returns a value greater than 32.
	return uintptr(res) > 32
}

windows_set_gamepad_vibration :: proc(gamepad: int, left: f32, right: f32) {
	if gamepad < 0 || gamepad >= MAX_GAMEPADS {
		return
	}

	vib := win32.XINPUT_VIBRATION {
		wLeftMotorSpeed = win32.WORD(left * 65535),
		wRightMotorSpeed = win32.WORD(right * 65535),
	}

	win32.XInputSetState(win32.XUSER(gamepad), &vib)
}

windows_set_internal_state :: proc(state: rawptr) {
	assert(state != nil)
	s = (^Windows_State)(state)
}

Windows_State :: struct {
	allocator: runtime.Allocator,
	custom_context: runtime.Context,
	hwnd: win32.HWND,
	window_mode: Window_Mode,

	screen_width: int,
	screen_height: int,

	window_scale: f32,

	in_resize_move_state: bool,
	screen_width_before_resize_move: int,
	screen_height_before_resize_move: int,

	minimized: bool,

	// left and right values for the triggers of each gamepad. We use this to known if a trigger has
	// been pressed/released like a button.
	previous_gamepad_triggers: [MAX_GAMEPADS][2]win32.BYTE,

	events: [dynamic]Event,
	mouse_locked: bool,
	cursor_hidden: bool,

	custom_cursors: hm.Dynamic_Handle_Map(Windows_Cursor, Custom_Cursor),

	// The cursor most recently passed to windows_set_cursor. The zero value is
	// Standard_Cursor.Default.
	current_cursor: Cursor,

	// for when returning from fullscreen to window mode
	restore_window_pos_x: int,
	restore_window_pos_y: int,
	restore_screen_width: int,
	restore_screen_height: int,

	window_render_glue: Window_Render_Glue,

	// Half of UTF-16 characters when it is a character when the character is outside the Basic
	// Multilingual Plane (BMP). Emojis are examples of such characters.
	char_high_surrogate: rune,
}

Windows_Cursor :: struct {
	handle: Custom_Cursor,
	hcursor: win32.HCURSOR,
}

windows_set_window_mode :: proc(window_mode: Window_Mode) {
	old_window_mode := s.window_mode
	s.window_mode = window_mode
	style := windows_get_style(window_mode)
	win32.SetWindowLongW(s.hwnd, win32.GWL_STYLE, i32(style))

	switch window_mode {
	case .Windowed, .Windowed_Resizable:
		r: win32.RECT
		set_window_pos_style: win32.DWORD = win32.SWP_NOACTIVATE | win32.SWP_NOZORDER

		if old_window_mode == .Borderless_Fullscreen {
			r.left = i32(s.restore_window_pos_x)
			r.top = i32(s.restore_window_pos_y)
			r.right = r.left + i32(s.restore_screen_width)
			r.bottom = r.top + i32(s.restore_screen_height)
		} else {
			r.left = 0
			r.top = 0
			r.right = i32(s.screen_width)
			r.bottom = i32(s.screen_height)
			set_window_pos_style |= win32.SWP_NOMOVE
		}

		win32.AdjustWindowRectExForDpi(&r, style, false, 0, win32.GetDpiForWindow(s.hwnd))

		win32.SetWindowPos(
			s.hwnd,
			{},
			i32(r.left),
			i32(r.top),
			i32(r.right - r.left),
			i32(r.bottom - r.top),
			set_window_pos_style,
		)

	case .Borderless_Fullscreen:
		mi := win32.MONITORINFO { cbSize = size_of (win32.MONITORINFO)}
		mon := win32.MonitorFromWindow(s.hwnd, .MONITOR_DEFAULTTONEAREST)

		if win32.GetMonitorInfoW(mon, &mi) {
			win32.SetWindowPos(s.hwnd, win32.HWND_TOP,
			mi.rcMonitor.left, mi.rcMonitor.top,
			mi.rcMonitor.right - mi.rcMonitor.left,
			mi.rcMonitor.bottom - mi.rcMonitor.top,
			win32.SWP_NOOWNERZORDER | win32.SWP_FRAMECHANGED)
		}
	}
}

windows_set_cursor_hidden :: proc(hidden: bool) {
	win32.ShowCursor(win32.BOOL(!hidden))
	s.cursor_hidden = hidden
}

windows_is_cursor_hidden :: proc() -> bool {
	return s.cursor_hidden
}

windows_set_mouse_locked :: proc(locked: bool) {
	s.mouse_locked = locked

	if locked {
		r: win32.RECT
		win32.GetClientRect(s.hwnd, &r)
		tl := win32.POINT{r.left, r.top}
		br := win32.POINT{r.right, r.bottom}
		win32.ClientToScreen(s.hwnd, &tl)
		win32.ClientToScreen(s.hwnd, &br)
		clip := win32.RECT{tl.x, tl.y, br.x, br.y}
		win32.ClipCursor(&clip)
		
		_windows_teleport_cursor_to_center()
	} else {
		win32.ClipCursor(nil)
	}
}

windows_is_mouse_locked :: proc() -> bool {
	return s.mouse_locked
}

_windows_teleport_cursor_to_center :: proc() {
	cx := s.screen_width / 2
	cy := s.screen_height / 2
	pt := win32.POINT{i32(cx), i32(cy)}
	win32.ClientToScreen(s.hwnd, &pt)
	win32.SetCursorPos(pt.x, pt.y)

	append(&s.events, Event_Mouse_Teleported {
		position = {f32(cx), f32(cy)},
	})
}

windows_create_custom_cursor :: proc(image: Image, hotspot: [2]int) -> Custom_Cursor {
	// CreateBitmap makes a device-dependent bitmap, which is not documented to support a real
	// alpha channel. A 32-bit cursor with alpha needs a DIB section instead: CreateDIBSection with
	// a BITMAPV5HEADER and explicit channel masks, which is also what GLFW and SDL do for this.
	header := win32.BITMAPV5HEADER {
		bV5Size        = size_of(win32.BITMAPV5HEADER),
		bV5Width       = i32(image.width),
		bV5Height      = -i32(image.height), // negative: top-down, matching Image's row order
		bV5Planes      = 1,
		bV5BitCount    = 32,
		bV5Compression = win32.BI_BITFIELDS,
		bV5RedMask     = 0x00ff0000,
		bV5GreenMask   = 0x0000ff00,
		bV5BlueMask    = 0x000000ff,
		bV5AlphaMask   = 0xff000000,
	}

	hdc := win32.GetDC(s.hwnd)

	dib_pixels: win32.PVOID
	h_color := win32.CreateDIBSection(
		hdc,
		(^win32.BITMAPINFO)(&header),
		win32.DIB_RGB_COLORS,
		&dib_pixels,
		nil,
		0,
	)

	// The DIB section owns its pixels, so the DC is only needed for the call above.
	win32.ReleaseDC(s.hwnd, hdc)

	if h_color == nil || dib_pixels == nil {
		log.errorf("CreateDIBSection failed with %v", win32.GetLastError())
		return {}
	}

	// We receive RGBA but the DIB, like GDI generally, wants BGRA.
	dib_colors := slice.from_ptr((^Color)(dib_pixels), len(image.pixels))
	for i in 0..<len(image.pixels) {
		src := image.pixels[i]
		dib_colors[i] = {src.b, src.g, src.r, src.a}
	}

	// The AND mask is expected even for a cursor with a real alpha channel, but every bit of it
	// should be 0 so it doesn't mask out anything the alpha channel already made transparent.
	// `make` zero-initializes, so there is nothing further to fill in.
	mask_stride := ((image.width + 31)/32)*4
	mask_bits := make([]u8, mask_stride*image.height, frame_allocator)

	h_mask := win32.CreateBitmap(i32(image.width), i32(image.height), 1, 1, raw_data(mask_bits))

	ii := win32.ICONINFO {
		fIcon    = false,
		xHotspot = u32(hotspot.x),
		yHotspot = u32(hotspot.y),
		hbmColor = h_color,
		hbmMask  = h_mask,
	}
	hcursor := (win32.HCURSOR)(win32.CreateIconIndirect(&ii))

	// CreateIconIndirect copies both bitmaps, so we own them again whether or not it worked.
	win32.DeleteObject(cast(win32.HGDIOBJ) h_color)
	win32.DeleteObject(cast(win32.HGDIOBJ) h_mask)

	if hcursor == nil {
		log.errorf("CreateIconIndirect failed with %v", win32.GetLastError())
		return {}
	}

	handle, add_err := hm.add(&s.custom_cursors, Windows_Cursor{hcursor = hcursor})

	if add_err != nil {
		log.errorf("Failed to create cursor. Error: %v", add_err)
		win32.DestroyCursor(hcursor)
		return {}
	}

	return handle
}

windows_set_cursor :: proc(cursor: Cursor) {
	// Reject a stale handle, so a programming error leaves the cursor alone.
	if c, is_custom := cursor.(Custom_Cursor); is_custom {
		if hm.get(&s.custom_cursors, c) == nil {
			log.errorf("Trying to set invalid cursor %v. It may have been destroyed.", c)
			return
		}
	}

	s.current_cursor = cursor
	windows_apply_cursor()
}

// Sets the OS cursor from s.current_cursor, falling back to the default arrow when a custom
// cursor no longer resolves (it was destroyed while on screen).
windows_apply_cursor :: proc() {
	handle: win32.HCURSOR

	switch c in s.current_cursor {
	case Standard_Cursor:
		// LoadCursor on a built-in IDC_ hands back a shared cursor owned by the system, so this
		// must not be destroyed and is cheap enough to look up each time.
		handle = win32.LoadCursorA(nil, windows_standard_cursor_id(c))
	case Custom_Cursor:
		if cd := hm.get(&s.custom_cursors, c); cd != nil {
			handle = cd.hcursor
		}
	}

	if handle == nil {
		handle = win32.LoadCursorA(nil, win32.IDC_ARROW)
	}

	win32.SetClassLongPtrW(s.hwnd, win32.GCLP_HCURSOR, (win32.LONG_PTR)(uintptr(handle)))
	win32.SetCursor(handle)
}

windows_standard_cursor_id :: proc(cursor: Standard_Cursor) -> cstring {
	switch cursor {
	case .Default:     return win32.IDC_ARROW
	case .Text:        return win32.IDC_IBEAM
	case .Hand:        return win32.IDC_HAND
	case .Crosshair:   return win32.IDC_CROSS
	case .Wait:        return win32.IDC_WAIT
	case .Progress:    return win32.IDC_APPSTARTING
	case .Resize_EW:   return win32.IDC_SIZEWE
	case .Resize_NS:   return win32.IDC_SIZENS
	case .Resize_NESW: return win32.IDC_SIZENESW
	case .Resize_NWSE: return win32.IDC_SIZENWSE
	case .Move:        return win32.IDC_SIZEALL
	case .Not_Allowed: return win32.IDC_NO
	}

	return win32.IDC_ARROW
}

windows_destroy_custom_cursor :: proc(custom_cursor: Custom_Cursor) {
	cd := hm.get(&s.custom_cursors, custom_cursor)

	if cd == nil {
		log.errorf(
			"Trying to destroy invalid cursor %v. It may already be destroyed.",
			custom_cursor,
		)
		return
	}

	win32.DestroyCursor(cd.hcursor)
	hm.remove(&s.custom_cursors, custom_cursor)

	// Falls back to the default if that was the cursor on screen.
	windows_apply_cursor()
}

s: ^Windows_State

_windows_window_proc :: proc "stdcall" (hwnd: win32.HWND, msg: win32.UINT, wparam: win32.WPARAM, lparam: win32.LPARAM) -> win32.LRESULT {
	context = s.custom_context

	switch msg {
	case win32.WM_DESTROY:
		win32.PostQuitMessage(0)

	case win32.WM_CLOSE:
		append(&s.events, Event_Close_Window_Requested{})

	case win32.WM_SYSKEYDOWN, win32.WM_KEYDOWN:
		repeat := bool(lparam & (1 << 30))
		key := key_from_event_params(wparam, lparam)

		if key != .None {
			if repeat {
				append(&s.events, Event_Key_Repeat {
					key = key,
				})
			} else {
				append(&s.events, Event_Key_Went_Down {
					key = key,
				})
			}
		}

	case win32.WM_SYSKEYUP, win32.WM_KEYUP:
		key := key_from_event_params(wparam, lparam)
		if key != .None {
			append(&s.events, Event_Key_Went_Up {
				key = key,
			})
		}

	case win32.WM_CHAR:
		// Note: We deliberately don't also handle WM_SYSCHAR here, since that message is sent for
		// character keys pressed while holding Alt (for example menu mnemonics), which shouldn't
		// be treated as text input.
		r := rune(wparam)

		if wparam >= 0xD800 && wparam <= 0xDBFF {
			// ^ High surrogate

			s.char_high_surrogate = r
		} else {
			codepoint: rune

			if wparam >= 0xDC00 && wparam <= 0xDFFF {
				// ^ Low surrogate

				if s.char_high_surrogate != 0 {
					codepoint = utf16.decode_surrogate_pair(s.char_high_surrogate, r)
				}
			} else if is_typable_rune(r) {
				codepoint = r
			}

			s.char_high_surrogate = 0

			if codepoint != 0 {
				append(&s.events, Event_Typed_Rune { typed = codepoint })
			}
		}

	case win32.WM_MOUSEMOVE:
		x := win32.GET_X_LPARAM(lparam)
		y := win32.GET_Y_LPARAM(lparam)

		if s.mouse_locked {
			cx := i32(s.screen_width / 2)
			cy := i32(s.screen_height / 2)

			if x != cx || y != cy {
				append(&s.events, Event_Mouse_Move {
					position = {f32(x), f32(y)},
				})

				_windows_teleport_cursor_to_center()
			}
		} else {
			append(&s.events, Event_Mouse_Move {
				position = {f32(x), f32(y)},
			})
		}

	case win32.WM_MOUSEWHEEL:
		delta := f32(win32.GET_WHEEL_DELTA_WPARAM(wparam))/win32.WHEEL_DELTA

		append(&s.events, Event_Mouse_Wheel {
			delta = delta,
		})

	case win32.WM_MOUSEHWHEEL:
		// Windows measures the horizontal wheel to the right, which is the direction we want.
		delta := f32(win32.GET_WHEEL_DELTA_WPARAM(wparam))/win32.WHEEL_DELTA

		append(&s.events, Event_Mouse_Wheel_Horizontal {
			delta = delta,
		})

	case win32.WM_LBUTTONDOWN:
		append(&s.events, Event_Mouse_Button_Went_Down {
			button = .Left,
		})
		win32.SetCapture(s.hwnd)

	case win32.WM_LBUTTONUP:
		append(&s.events, Event_Mouse_Button_Went_Up {
			button = .Left,
		})
		win32.ReleaseCapture()

	case win32.WM_MBUTTONDOWN:
		append(&s.events, Event_Mouse_Button_Went_Down {
			button = .Middle,
		})
		win32.SetCapture(s.hwnd)

	case win32.WM_MBUTTONUP:
		append(&s.events, Event_Mouse_Button_Went_Up {
			button = .Middle,
		})
		win32.ReleaseCapture()

	case win32.WM_RBUTTONDOWN:
		append(&s.events, Event_Mouse_Button_Went_Down {
			button = .Right,
		})
		win32.SetCapture(s.hwnd)

	case win32.WM_RBUTTONUP:
		append(&s.events, Event_Mouse_Button_Went_Up {
			button = .Right,
		})
		win32.ReleaseCapture()

	case win32.WM_MOVE:
		if s.window_mode == .Windowed || s.window_mode == .Windowed_Resizable {
			x := win32.GET_X_LPARAM(lparam)
			y := win32.GET_Y_LPARAM(lparam)

			s.restore_window_pos_x = int(x)
			s.restore_window_pos_y = int(y)
		}

	case win32.WM_DPICHANGED:
		new_dpi := win32.LOWORD(wparam)
		s.window_scale = f32(new_dpi) / 96.0

		append(&s.events, Event_Window_Scale_Changed {
			scale = s.window_scale,
			screen_width = s.screen_width,
			screen_height = s.screen_height,
		})

	case win32.WM_ENTERSIZEMOVE:
		s.in_resize_move_state = true
		s.screen_width_before_resize_move = s.screen_width
		s.screen_height_before_resize_move = s.screen_height

	case win32.WM_EXITSIZEMOVE:
		s.in_resize_move_state = false

		if s.screen_width_before_resize_move != s.screen_width ||
		   s.screen_height_before_resize_move != s.screen_height {
			append(&s.events, Event_Screen_Resize {
				width = s.screen_width,
				height = s.screen_height,
			})
		}

	case win32.WM_SIZE:
		// When the window is minimized, Windows reports the client area as 0x0. Ignore that and
		// keep reporting the last known screen size, so a 0x0 size never propagates into the
		// swapchain, the projection matrix or `get_screen_size`.
		if wparam == win32.SIZE_MINIMIZED {
			s.minimized = true
			return win32.DefWindowProcW(hwnd, msg, wparam, lparam)
		}

		s.minimized = false

		width := win32.LOWORD(lparam)
		height := win32.HIWORD(lparam)

		s.screen_width = int(width)
		s.screen_height = int(height)

		if s.window_mode == .Windowed || s.window_mode == .Windowed_Resizable {
			s.restore_screen_width = s.screen_width
			s.restore_screen_height = s.screen_height
		}

		// We are actively resizing or moving the window, we'll save the event for later so it does
		// not get spammy.
		if !s.in_resize_move_state {
			append(&s.events, Event_Screen_Resize {
				width = s.screen_width,
				height = s.screen_height,
			})
		}

	case win32.WM_SETFOCUS:
		append(&s.events, Event_Window_Focused {})

	case win32.WM_KILLFOCUS:
		s.mouse_locked = false
		if s.cursor_hidden {
			win32.ShowCursor(true)
			s.cursor_hidden = false
		}
		append(&s.events, Event_Window_Unfocused {})
	}

	return win32.DefWindowProcW(hwnd, msg, wparam, lparam)
}

key_from_event_params :: proc(wparam: win32.WPARAM, lparam: win32.LPARAM) -> Keyboard_Key{
	switch wparam {
	case win32.VK_SHIFT:
		scancode := (lparam & 0x00ff0000) >> 16
		new_vk := win32.MapVirtualKeyW(u32(scancode), win32.MAPVK_VSC_TO_VK_EX)
		return new_vk == win32.VK_LSHIFT ? .Left_Shift : .Right_Shift

	case win32.VK_CONTROL:
		is_right := win32.HIWORD(lparam) & win32.KF_EXTENDED != 0
		return is_right ? .Right_Control : .Left_Control

	case win32.VK_MENU:
		is_right := win32.HIWORD(lparam) & win32.KF_EXTENDED != 0
		return is_right ? .Right_Alt : .Left_Alt

	case win32.VK_RETURN:
		if win32.HIWORD(lparam) & win32.KF_EXTENDED != 0 {
			return .NP_Enter
		}
	}

	if wparam >= len(WIN32_VK_MAP) {
		return .None
	}

	return WIN32_VK_MAP[wparam]
}

WIN32_VK_MAP := [255]Keyboard_Key {
	win32.VK_0 = .N0,
	win32.VK_1 = .N1,
	win32.VK_2 = .N2,
	win32.VK_3 = .N3,
	win32.VK_4 = .N4,
	win32.VK_5 = .N5,
	win32.VK_6 = .N6,
	win32.VK_7 = .N7,
	win32.VK_8 = .N8,
	win32.VK_9 = .N9,

	win32.VK_A = .A,
	win32.VK_B = .B,
	win32.VK_C = .C,
	win32.VK_D = .D,
	win32.VK_E = .E,
	win32.VK_F = .F,
	win32.VK_G = .G,
	win32.VK_H = .H,
	win32.VK_I = .I,
	win32.VK_J = .J,
	win32.VK_K = .K,
	win32.VK_L = .L,
	win32.VK_M = .M,
	win32.VK_N = .N,
	win32.VK_O = .O,
	win32.VK_P = .P,
	win32.VK_Q = .Q,
	win32.VK_R = .R,
	win32.VK_S = .S,
	win32.VK_T = .T,
	win32.VK_U = .U,
	win32.VK_V = .V,
	win32.VK_W = .W,
	win32.VK_X = .X,
	win32.VK_Y = .Y,
	win32.VK_Z = .Z,

	win32.VK_OEM_7      = .Apostrophe,
	win32.VK_OEM_COMMA  = .Comma,
	win32.VK_OEM_MINUS  = .Minus,
	win32.VK_OEM_PERIOD = .Period,
	win32.VK_OEM_2      = .Slash,
	win32.VK_OEM_1      = .Semicolon,
	win32.VK_OEM_PLUS   = .Equal,
	win32.VK_OEM_4      = .Left_Bracket,
	win32.VK_OEM_5      = .Backslash,
	win32.VK_OEM_6      = .Right_Bracket,
	win32.VK_OEM_3      = .Backtick,

	win32.VK_SPACE   = .Space,
	win32.VK_ESCAPE  = .Escape,
	win32.VK_RETURN  = .Enter,
	win32.VK_TAB     = .Tab,
	win32.VK_BACK    = .Backspace,
	win32.VK_INSERT  = .Insert,
	win32.VK_DELETE  = .Delete,
	win32.VK_RIGHT   = .Right,
	win32.VK_LEFT    = .Left,
	win32.VK_DOWN    = .Down,
	win32.VK_UP      = .Up,
	win32.VK_PRIOR   = .Page_Up,
	win32.VK_NEXT    = .Page_Down,
	win32.VK_HOME    = .Home,
	win32.VK_END     = .End,
	win32.VK_CAPITAL = .Caps_Lock,
	win32.VK_SCROLL  = .Scroll_Lock,
	win32.VK_NUMLOCK = .Num_Lock,
	win32.VK_PRINT   = .Print_Screen,
	win32.VK_PAUSE   = .Pause,

	win32.VK_F1  = .F1,
	win32.VK_F2  = .F2,
	win32.VK_F3  = .F3,
	win32.VK_F4  = .F4,
	win32.VK_F5  = .F5,
	win32.VK_F6  = .F6,
	win32.VK_F7  = .F7,
	win32.VK_F8  = .F8,
	win32.VK_F9  = .F9,
	win32.VK_F10 = .F10,
	win32.VK_F11 = .F11,
	win32.VK_F12 = .F12,

	// Alt, shift and control are handled in key_from_event_params
	win32.VK_LWIN     = .Left_Super,
	win32.VK_RWIN     = .Right_Super,
	win32.VK_APPS     = .Menu,

	win32.VK_NUMPAD0 = .NP_0,
	win32.VK_NUMPAD1 = .NP_1,
	win32.VK_NUMPAD2 = .NP_2,
	win32.VK_NUMPAD3 = .NP_3,
	win32.VK_NUMPAD4 = .NP_4,
	win32.VK_NUMPAD5 = .NP_5,
	win32.VK_NUMPAD6 = .NP_6,
	win32.VK_NUMPAD7 = .NP_7,
	win32.VK_NUMPAD8 = .NP_8,
	win32.VK_NUMPAD9 = .NP_9,
	
	win32.VK_DECIMAL = .NP_Decimal,
	win32.VK_DIVIDE  = .NP_Divide,
	win32.VK_MULTIPLY = .NP_Multiply,
	win32.VK_SUBTRACT = .NP_Subtract,
	win32.VK_ADD = .NP_Add,

	// NP_Enter is handled separately

	win32.VK_OEM_NEC_EQUAL = .NP_Equal,
}
