#+build js
#+vet explicit-allocators
#+feature dynamic-literals
#+private file

package karl2d

@(private="package")
PLATFORM_WEB :: Platform_Interface {
	state_size = web_state_size,
	init = web_init,
	shutdown = web_shutdown,
	get_window_render_glue = web_get_window_render_glue,
	get_events = web_get_events,
	set_window_title = web_set_window_title,
	get_clipboard_text = web_get_clipboard_text,
	set_clipboard_text = web_set_clipboard_text,
	set_screen_size = web_set_screen_size,
	get_screen_width = web_get_screen_width,
	get_screen_height = web_get_screen_height,
	set_window_position = web_set_position,
	get_window_position = web_get_position,
	get_window_scale = web_get_window_scale,
	set_window_mode = web_set_window_mode,

	set_cursor_hidden = web_set_cursor_hidden,
	is_cursor_hidden = web_is_cursor_hidden,
	set_mouse_locked = web_set_mouse_locked,
	is_mouse_locked = web_is_mouse_locked,
	create_custom_cursor = web_create_custom_cursor,
	set_cursor = web_set_cursor,
	destroy_custom_cursor = web_destroy_custom_cursor,

	is_gamepad_active = web_is_gamepad_active,
	get_gamepad_axis = web_get_gamepad_axis,
	set_gamepad_vibration = web_set_gamepad_vibration,

	open_url = web_open_url,

	set_internal_state = web_set_internal_state,
}

import "core:sys/wasm/js"
import "core:math"
import "core:encoding/base64"
import "base:runtime"
import hm "core:container/handle_map"
import "log"
import "core:fmt"

web_state_size :: proc() -> int {
	return size_of(Web_State)
}

web_init :: proc(
	window_state: rawptr,
	window_width: int,
	window_height: int,
	window_title: string,
	init_options: Init_Options,
	allocator: runtime.Allocator,
) {
	s = (^Web_State)(window_state)
	s.allocator = allocator
	s.events = make([dynamic]Event, allocator)
	s.key_from_js_event_key_code = make(map[string]Keyboard_Key, allocator)
	s.canvas_id = "webgl-canvas"
	hm.dynamic_init(&s.custom_cursors, allocator)

	js.set_document_title(window_title)
	s.prev_scale = f32(js.device_pixel_ratio())
	// The browser window probably has some other size than what was sent in.
	switch init_options.window_mode {
	case .Windowed:
		web_set_screen_size(window_width, window_height)
	case .Windowed_Resizable:
		web_set_screen_size_to_window_size(s.canvas_id)
	case .Borderless_Fullscreen:
		log.error("Borderless_Fullscreen not implemented on web, but you can make it happen by using Window_Mode.Windowed_Resizable and putting the game in a fullscreen iframe.")
	}

	s.window_mode = init_options.window_mode

	add_window_event_listener(.Resize, web_event_window_resize)
	add_canvas_event_listener(.Mouse_Move, web_event_mouse_move)
	add_canvas_event_listener(.Mouse_Down, web_event_mouse_down)
	add_window_event_listener(.Mouse_Up, web_event_mouse_up)
	add_canvas_event_listener(.Wheel, web_event_mouse_wheel)

	add_window_event_listener(.Key_Down, web_event_key_down)
	add_window_event_listener(.Key_Up, web_event_key_up)
	add_window_event_listener(.Focus, web_event_focus)
	add_window_event_listener(.Blur, web_event_blur)
	add_window_event_listener(.Key_Press, web_event_key_press)

	add_window_event_listener(.Pointer_Lock_Change, _web_event_pointer_lock_change)

	if init_options.disable_auto_scale_hint {
		log.warn("disable_auto_scale_hint not supported on web")
	}
}

web_event_key_down :: proc(e: js.Event) {
	key := key_from_js_event(e)

	if key == .None {
		return
	}

	if e.key.repeat {
		append(&s.events, Event_Key_Repeat {
			key = key,
		})
	} else {
		append(&s.events, Event_Key_Went_Down {
			key = key,
		})
	}
}

web_event_key_up :: proc(e: js.Event) {
	key := key_from_js_event(e)
	append(&s.events, Event_Key_Went_Up {
		key = key,
	})
}

// Note: `e.key.char` comes from the deprecated `keypress` DOM event's `charCode`, which is a
// single UTF-16 code unit. This means characters outside the Basic Multilingual Plane (such as
// emoji) can't be represented and won't come through here.
web_event_key_press :: proc(e: js.Event) {
	r := e.key.char

	if is_typable_rune(r) {
		append(&s.events, Event_Typed_Rune { typed = r })
	}
}

web_event_focus :: proc(e: js.Event) {
	append(&s.events, Event_Window_Focused {})
}

web_event_blur :: proc(e: js.Event) {
	s.mouse_locked = false
	append(&s.events, Event_Window_Unfocused {})
}

web_event_window_resize :: proc(e: js.Event) {
	new_scale := f32(js.device_pixel_ratio())

	// We get a window resize event on DPI scale change. Therefore we can piggyback on this to do
	// send the event about the DPI changing.
	if new_scale != s.prev_scale {
		s.prev_scale = new_scale
		web_set_screen_size(s.width, s.height)
		append(&s.events, Event_Window_Scale_Changed {
			scale = new_scale,
			screen_width = s.width,
			screen_height = s.height,
		})
	}

	if s.window_mode == .Windowed_Resizable {
		web_set_screen_size_to_window_size(s.canvas_id)
	}
}

web_event_mouse_move :: proc(e: js.Event) {
	if s.mouse_locked {
		cx := f32(s.width / 2)
		cy := f32(s.height / 2)
		dx := f32(e.mouse.movement.x) * f32(js.device_pixel_ratio())
		dy := f32(e.mouse.movement.y) * f32(js.device_pixel_ratio())
		append(&s.events, Event_Mouse_Move { position = {cx + dx, cy + dy} })
		append(&s.events, Event_Mouse_Teleported { position = {cx, cy} })
	} else {
		append(&s.events, Event_Mouse_Move {
			position = {
				math.floor(f32(e.mouse.client.x) * f32(js.device_pixel_ratio())),
				math.floor(f32(e.mouse.client.y) * f32(js.device_pixel_ratio())),
			},
		})
	}
}

web_event_mouse_down :: proc(e: js.Event) {
	button := Mouse_Button.Left

	if e.mouse.button == 2 {
		button = .Right
	}

	if e.mouse.button == 1 {
		button = .Middle 
	}

	append(&s.events, Event_Mouse_Button_Went_Down {
		button = button,
	})
}

web_event_mouse_up :: proc(e: js.Event) {
	button := Mouse_Button.Left

	if e.mouse.button == 2 {
		button = .Right
	}

	if e.mouse.button == 1 {
		button = .Middle 
	}

	append(&s.events, Event_Mouse_Button_Went_Up {
		button = button,
	})
}

web_event_mouse_wheel :: proc(e: js.Event) {
	// Not the best way, but how would we know what the wheel deltaMode really represents? If it is
	// in pixels, how much "scroll" does that equal to? So we keep the direction and call it one
	// click. The browser measures down and right as positive, so the vertical axis is flipped.
	// A swipe along one axis reports zero on the other, which is not worth an event.
	if e.wheel.delta.y != 0 {
		append(&s.events, Event_Mouse_Wheel {
			delta = e.wheel.delta.y > 0 ? -1 : 1,
		})
	}

	if e.wheel.delta.x != 0 {
		append(&s.events, Event_Mouse_Wheel_Horizontal {
			delta = e.wheel.delta.x > 0 ? 1 : -1,
		})
	}
}

add_canvas_event_listener :: proc(evt: js.Event_Kind, callback: proc(e: js.Event)) {
	js.add_event_listener(
		s.canvas_id, 
		evt, 
		nil, 
		callback,
		true,
	)
}

add_window_event_listener :: proc(evt: js.Event_Kind, callback: proc(e: js.Event)) {
	js.add_window_event_listener(evt, nil, callback, true)
}

remove_window_event_listener :: proc(evt: js.Event_Kind, callback: proc(e: js.Event)) {
	js.remove_window_event_listener(evt, nil, callback, true)
}

web_set_screen_size_to_window_size :: proc(canvas_id: HTML_Canvas_ID) {
	rect := js.get_bounding_client_rect("body")
	
	scale := web_get_window_scale()
	s.width = int(f32(rect.width) * scale)
	s.height = int(f32(rect.height) * scale)

	js.set_element_key_f64(canvas_id, "width", f64(s.width))
	js.set_element_key_f64(canvas_id, "height", f64(s.height))

	js.set_element_style(canvas_id, "width", fmt.tprintf("%fpx", f64(rect.width)))
	js.set_element_style(canvas_id, "height", fmt.tprintf("%fpx", f64(rect.height)))

	append(&s.events, Event_Screen_Resize {
		width = s.width,
		height = s.height,
	})
}

web_shutdown :: proc() {
	for it := hm.dynamic_iterator_make(&s.custom_cursors); cd, _ in hm.dynamic_iterate(&it) {
		delete(cd.data_uri, s.allocator)
		delete(cd.style_value, s.allocator)
		delete(cd.style_value_scaled, s.allocator)
	}
	hm.dynamic_destroy(&s.custom_cursors)

	delete(s.events)
	delete(s.key_from_js_event_key_code)
}

web_get_window_render_glue :: proc() -> Window_Render_Glue {
	// We can only use WebGL backend right now, so this is very simple: Just pass canvas ID as
	// state, the WebGL backend knows to convert it properly.
	return {
		state = (^Window_Render_Glue_State)(&s.canvas_id),
	}
}

// This works for XBox controller -- does it work for PlayStation?
//
// The magic numbers are from https://gamepad-tester.net/
KARL2D_GAMEPAD_BUTTON_FROM_JS :: [Gamepad_Button]int {
	.None = 0,
	
	.Left_Face_Up = 12,
	.Left_Face_Down = 13,
	.Left_Face_Left = 14,
	.Left_Face_Right = 15,

	.Right_Face_Up = 3, 
	.Right_Face_Down = 0, 
	.Right_Face_Left = 2, 
	.Right_Face_Right = 1, 

	.Left_Shoulder = 4,
	.Left_Trigger = 6,

	.Right_Shoulder = 5,
	.Right_Trigger = 7,

	.Left_Stick_Press = 10, 
	.Right_Stick_Press = 11, 

	.Middle_Face_Left = 8, 
	.Middle_Face_Middle = -1, 
	.Middle_Face_Right = 9, 
}

web_get_events :: proc(events: ^[dynamic]Event) {
	append(events, ..s.events[:])
	runtime.clear(&s.events)

	for gamepad_idx in 0..<MAX_GAMEPADS {
		// new_state
		ns: js.Gamepad_State

		if !js.get_gamepad_state(gamepad_idx, &ns) || !ns.connected {
			if s.gamepad_state[gamepad_idx].connected {
				s.gamepad_state[gamepad_idx] = {}
			}
			continue
		}

		// prev_state
		ps := s.gamepad_state[gamepad_idx]

		// We check if any button changed from pressed to not pressed and the other way around.
		for js_idx, button in KARL2D_GAMEPAD_BUTTON_FROM_JS {
			if js_idx == -1 {
				continue
			}

			if !ps.buttons[js_idx].pressed && ns.buttons[js_idx].pressed {
				append(events, Event_Gamepad_Button_Went_Down {
					gamepad = gamepad_idx,
					button = button,
				})
			}

			if ps.buttons[js_idx].pressed && !ns.buttons[js_idx].pressed {
				append(events, Event_Gamepad_Button_Went_Up {
					gamepad = gamepad_idx,
					button = button,
				})
			}
		}

		s.gamepad_state[gamepad_idx] = ns
	}
}

web_get_screen_width :: proc() -> int {
	return s.width
}

web_get_screen_height :: proc() -> int {
	return s.height
}

web_clear_events :: proc() {
	runtime.clear(&s.events)
}

web_set_window_title :: proc(title: string) {
	js.set_document_title(title)
}

web_get_clipboard_text :: proc(allocator: runtime.Allocator) -> (string, bool) {
	return {}, false
}

web_set_clipboard_text :: proc(text: string) -> bool {
	return false
}

web_set_position :: proc(x: int, y: int) {
	log.warn("set_window_position not implemented on web")
}

web_get_position :: proc() -> Vec2 {
	log.warn("get_window_position not implemented on web")
	return {}
}

web_set_screen_size :: proc(w, h: int) {
	scale := web_get_window_scale()
	s.width = int(f32(w) * scale)
	s.height = int(f32(h) * scale)

	js.set_element_key_f64(s.canvas_id, "width", f64(s.width))
	js.set_element_key_f64(s.canvas_id, "height", f64(s.height))

	js.set_element_style(s.canvas_id, "width",  fmt.tprintf("%fpx", f64(w)))
	js.set_element_style(s.canvas_id, "height", fmt.tprintf("%fpx", f64(h)))
}

web_get_window_scale :: proc() -> f32 {
	return f32(js.device_pixel_ratio())
}

web_set_window_mode :: proc(new_mode: Window_Mode) {
	if new_mode == .Borderless_Fullscreen {
		log.error("Borderless_Fullscreen not implemented on web, but you can make it happen by using Window_Mode.Windowed_Resizable and putting the game in a fullscreen iframe.")
		return
	}

	old_mode := s.window_mode
	s.window_mode = new_mode

	if new_mode == .Windowed_Resizable && old_mode == .Windowed {
		web_set_screen_size_to_window_size(s.canvas_id)
	} else if new_mode == .Windowed && old_mode == .Windowed_Resizable {
		web_set_screen_size(s.width, s.height)
	}
}

web_set_cursor_hidden :: proc(hidden: bool) {
	s.cursor_hidden = hidden
	web_apply_cursor()
}

web_is_cursor_hidden :: proc() -> bool {
	return s.cursor_hidden
}

_web_event_pointer_lock_change :: proc(e: js.Event) {
	js.evaluate("document.getElementById('webgl-canvas')._pointerLocked = document.pointerLockElement !== null ? 1 : 0")
	s.mouse_locked = js.get_element_key_f64("webgl-canvas", "_pointerLocked") != 0
}

web_set_mouse_locked :: proc(locked: bool) {
	if locked {
		js.evaluate("document.getElementById('webgl-canvas').requestPointerLock()")
		cx := f32(s.width / 2)
		cy := f32(s.height / 2)
		append(&s.events, Event_Mouse_Teleported { position = {cx, cy} })
	} else {
		js.evaluate("document.exitPointerLock()")
	}

	// s.mouse_locked set by _web_event_pointer_lock_change
}

web_is_mouse_locked :: proc() -> bool {
	return s.mouse_locked
}

web_create_custom_cursor :: proc(image: Image, hotspot: [2]int) -> Custom_Cursor {
	// There is no hardware cursor API on the web, so we hand the browser a PNG as a data URI and
	// let CSS do the work. core:image/png can only decode, not encode, so we encode it ourselves;
	// see encode_png's own comment for why that is fine here.
	png_bytes, encode_ok := encode_png(image, frame_allocator)
	if !encode_ok {
		log.error("Failed to encode cursor image as PNG")
		return {}
	}

	// Browsers cap `cursor` images at 128 CSS pixels; anything bigger is either clamped or, in
	// Firefox, ignored entirely and silently replaced with the default cursor.
	scale := web_get_window_scale()
	if f32(image.width)/scale > 128 || f32(image.height)/scale > 128 {
		log.warnf(
			"Cursor image is %vx%v physical pixels, which is %.0fx%.0f CSS pixels at the current " +
			"%vx display scale. Browsers cap cursors at 128 CSS pixels and some ignore anything " +
			"bigger entirely, so consider using a smaller image.",
			image.width, image.height,
			f32(image.width)/scale, f32(image.height)/scale,
			scale,
		)
	}

	// The base64 is copied into the data URI below, so it is not needed after that.
	pixels_b64 := base64.encode(png_bytes, allocator = frame_allocator)

	cursor := Web_Cursor{hotspot = hotspot}
	cursor.data_uri = fmt.aprintf(
		"data:image/png;base64,%v",
		pixels_b64,
		allocator = s.allocator,
	)
	web_build_cursor_style(&cursor)

	handle, add_err := hm.add(&s.custom_cursors, cursor)

	if add_err != nil {
		log.errorf("Failed to create cursor. Error: %v", add_err)
		delete(cursor.data_uri, s.allocator)
		delete(cursor.style_value, s.allocator)
		delete(cursor.style_value_scaled, s.allocator)
		return {}
	}

	return handle
}

// The `cursor` property sizes its image in CSS pixels, but the rest of Karl2D works in device
// pixels: the canvas is `scale` times bigger than its CSS box. A cursor made from a 128x128 image
// would therefore be drawn twice as big as a 128x128 sprite drawn by the game on a 2x display.
//
// `image-set` fixes that by telling the browser the image has `scale` device pixels per CSS pixel,
// which both scales it down and keeps it crisp. The hotspot is in CSS pixels too, so it is scaled
// to match. We keep the plain `url` value around as a fallback, see `web_set_cursor`.
web_build_cursor_style :: proc(cursor: ^Web_Cursor) {
	delete(cursor.style_value, s.allocator)
	delete(cursor.style_value_scaled, s.allocator)

	scale := web_get_window_scale()

	cursor.style_value = fmt.aprintf(
		"url(%v) %v %v, auto",
		cursor.data_uri,
		cursor.hotspot.x, cursor.hotspot.y,
		allocator = s.allocator,
	)

	cursor.style_value_scaled = fmt.aprintf(
		"image-set(url(%v) %vx) %.2f %.2f, auto",
		cursor.data_uri, scale,
		f32(cursor.hotspot.x) / scale, f32(cursor.hotspot.y) / scale,
		allocator = s.allocator,
	)

	cursor.built_for_scale = scale
}

web_set_cursor :: proc(cursor: Cursor) {
	// Reject a stale handle, so a programming error leaves the cursor alone.
	if c, is_custom := cursor.(Custom_Cursor); is_custom {
		if hm.get(&s.custom_cursors, c) == nil {
			log.errorf("Trying to set invalid cursor %v. It may have been destroyed.", c)
			return
		}
	}

	s.current_cursor = cursor
	web_apply_cursor()
}

web_standard_cursor_keyword :: proc(cursor: Standard_Cursor) -> string {
	switch cursor {
	case .Default:     return "default"
	case .Text:        return "text"
	case .Hand:        return "pointer"
	case .Crosshair:   return "crosshair"
	case .Wait:        return "wait"
	case .Progress:    return "progress"
	case .Resize_EW:   return "ew-resize"
	case .Resize_NS:   return "ns-resize"
	case .Resize_NESW: return "nesw-resize"
	case .Resize_NWSE: return "nwse-resize"
	case .Move:        return "move"
	case .Not_Allowed: return "not-allowed"
	}

	return "default"
}

// Applies s.cursor_hidden and s.current_cursor to the canvas. The two share the same CSS `cursor`
// property, so every entry point goes through this.
web_apply_cursor :: proc() {
	if s.cursor_hidden {
		js.set_element_style(s.canvas_id, "cursor", "none")
		s.applied_cursor = nil
		return
	}

	switch c in s.current_cursor {
	case Standard_Cursor:
		// No dedup here: a keyword is tiny, unlike the data URI below.
		js.set_element_style(s.canvas_id, "cursor", web_standard_cursor_keyword(c))
		s.applied_cursor = nil

	case Custom_Cursor:
		cd := hm.get(&s.custom_cursors, c)

		if cd == nil {
			js.set_element_style(s.canvas_id, "cursor", web_standard_cursor_keyword(.Default))
			s.applied_cursor = nil
			return
		}

		scale := web_get_window_scale()

		if cd.built_for_scale != scale {
			web_build_cursor_style(cd)
		}

		// The data URI is tens of kilobytes and games tend to set the cursor every frame, so don't
		// make the browser re-parse it when nothing has changed.
		if s.applied_cursor == cd && s.applied_scale == scale {
			return
		}

		// Assign the plain value first. Browsers that can't parse `image-set` ignore the second
		// assignment and keep this one, which is the right thing to fall back to.
		js.set_element_style(s.canvas_id, "cursor", cd.style_value)
		js.set_element_style(s.canvas_id, "cursor", cd.style_value_scaled)

		s.applied_cursor = cd
		s.applied_scale = scale
	}
}

web_destroy_custom_cursor :: proc(custom_cursor: Custom_Cursor) {
	cd := hm.get(&s.custom_cursors, custom_cursor)

	if cd == nil {
		log.errorf(
			"Trying to destroy invalid cursor %v. It may already be destroyed.",
			custom_cursor,
		)
		return
	}

	delete(cd.data_uri, s.allocator)
	delete(cd.style_value, s.allocator)
	delete(cd.style_value_scaled, s.allocator)
	hm.remove(&s.custom_cursors, custom_cursor)

	// Falls back to the default if that was the cursor on screen.
	web_apply_cursor()
}

web_is_gamepad_active :: proc(gamepad: int) -> bool {
	if gamepad < 0 || gamepad >= MAX_GAMEPADS {
		return false
	}

	return s.gamepad_state[gamepad].connected
}

web_get_gamepad_axis :: proc(gamepad: int, axis: Gamepad_Axis) -> f32 {
	if gamepad < 0 || gamepad >= MAX_GAMEPADS {
		return 0
	}

	if axis == .Left_Trigger {
		return f32(s.gamepad_state[gamepad].buttons[KARL2D_GAMEPAD_BUTTON_FROM_JS[.Left_Trigger]].value)
	}

	if axis == .Right_Trigger {
		return f32(s.gamepad_state[gamepad].buttons[KARL2D_GAMEPAD_BUTTON_FROM_JS[.Right_Trigger]].value)
	}

	js_axis: int

	switch axis {
	case .None: return 0
	case .Left_Stick_X: js_axis = 0
	case .Left_Stick_Y: js_axis = 1
	case .Right_Stick_X: js_axis = 2
	case .Right_Stick_Y: js_axis = 3
	case .Left_Trigger: return 0 // virtually unreachable
	case .Right_Trigger: return 0 // virtually unreachable
	}

	return f32(s.gamepad_state[gamepad].axes[js_axis])
}

web_set_gamepad_vibration :: proc(gamepad: int, left: f32, right: f32) {
	if gamepad < 0 || gamepad >= MAX_GAMEPADS {
		return
	}
}

web_open_url :: proc(url: string) -> bool {
	js.open(url)
	return true
}

web_set_internal_state :: proc(state: rawptr) {
	assert(state != nil)
	s = (^Web_State)(state)
}

@(private="package")
HTML_Canvas_ID :: string

Web_State :: struct {
	allocator: runtime.Allocator,
	canvas_id: HTML_Canvas_ID,
	width: int,
	height: int,
	prev_scale: f32,
	events: [dynamic]Event,
	mouse_locked: bool,
	cursor_hidden: bool,

	custom_cursors: hm.Dynamic_Handle_Map(Web_Cursor, Custom_Cursor),

	// The cursor most recently passed to web_set_cursor. The zero value is Standard_Cursor.Default.
	current_cursor: Cursor,

	// What web_apply_cursor last wrote to the canvas, so it can skip redundant work. Safe to hold
	// across frames: the handle map allocates new chunks rather than moving existing ones, and
	// web_apply_cursor clears this whenever the cursor it points at stops resolving.
	applied_cursor: ^Web_Cursor,
	applied_scale: f32,
	gamepad_state: [MAX_GAMEPADS]js.Gamepad_State,
	window_mode: Window_Mode,
	key_from_js_event_key_code: map[string]Keyboard_Key,
}

Web_Cursor :: struct {
	handle: Custom_Cursor,
	data_uri: string,
	hotspot: [2]int,

	// Both CSS values for `data_uri`, rebuilt whenever the DPI scale changes. See
	// `web_build_cursor_style`.
	style_value: string,
	style_value_scaled: string,
	built_for_scale: f32,
}

s: ^Web_State

key_from_js_event :: proc(e: js.Event) -> Keyboard_Key {
	if len(s.key_from_js_event_key_code) == 0 {
		context.allocator = s.allocator
		s.key_from_js_event_key_code = {
			"Digit1" = .N1,
			"Digit2" = .N2,
			"Digit3" = .N3,
			"Digit4" = .N4,
			"Digit5" = .N5,
			"Digit6" = .N6,
			"Digit7" = .N7,
			"Digit8" = .N8,
			"Digit9" = .N9,
			"Digit0" = .N0,

			"KeyA" = .A,
			"KeyB" = .B,
			"KeyC" = .C,
			"KeyD" = .D,
			"KeyE" = .E,
			"KeyF" = .F,
			"KeyG" = .G,
			"KeyH" = .H,
			"KeyI" = .I,
			"KeyJ" = .J,
			"KeyK" = .K,
			"KeyL" = .L,
			"KeyM" = .M,
			"KeyN" = .N,
			"KeyO" = .O,
			"KeyP" = .P,
			"KeyQ" = .Q,
			"KeyR" = .R,
			"KeyS" = .S,
			"KeyT" = .T,
			"KeyU" = .U,
			"KeyV" = .V,
			"KeyW" = .W,
			"KeyX" = .X,
			"KeyY" = .Y,
			"KeyZ" = .Z,

			"Quote" = .Apostrophe,
			"Comma" = .Comma,
			"Minus" = .Minus,
			"Period" = .Period,
			"Slash" = .Slash,
			"Semicolon" = .Semicolon,
			"Equal" = .Equal,
			"BracketLeft" = .Left_Bracket,
			"Backslash" = .Backslash,
			"IntlBackslash" = .Backslash,
			"BracketRight" = .Right_Bracket,
			"Backquote" = .Backtick,

			"Space" = .Space,
			"Escape" = .Escape,
			"Enter" = .Enter,
			"Tab" = .Tab,
			"Backspace" = .Backspace,
			"Insert" = .Insert,
			"Delete" = .Delete,
			"ArrowRight" = .Right,
			"ArrowLeft" = .Left,
			"ArrowDown" = .Down,
			"ArrowUp" = .Up,
			"PageUp" = .Page_Up,
			"PageDown" = .Page_Down,
			"Home" = .Home,
			"End" = .End,
			"CapsLock" = .Caps_Lock,
			"ScrollLock" = .Scroll_Lock,
			"NumLock" = .Num_Lock,
			"PrintScreen" = .Print_Screen,
			"Pause" = .Pause,

			"F1" = .F1,
			"F2" = .F2,
			"F3" = .F3,
			"F4" = .F4,
			"F5" = .F5,
			"F6" = .F6,
			"F7" = .F7,
			"F8" = .F8,
			"F9" = .F9,
			"F10" = .F10,
			"F11" = .F11,
			"F12" = .F12,

			"ShiftLeft" = .Left_Shift,
			"ControlLeft" = .Left_Control,
			"AltLeft" = .Left_Alt,
			"MetaLeft" = .Left_Super,
			"ShiftRight" = .Right_Shift,
			"ControlRight" = .Right_Control,
			"AltRight" = .Right_Alt,
			"MetaRight" = .Right_Super,
			"ContextMenu" = .Menu,

			"Numpad0" = .NP_0,
			"Numpad1" = .NP_1,
			"Numpad2" = .NP_2,
			"Numpad3" = .NP_3,
			"Numpad4" = .NP_4,
			"Numpad5" = .NP_5,
			"Numpad6" = .NP_6,
			"Numpad7" = .NP_7,
			"Numpad8" = .NP_8,
			"Numpad9" = .NP_9,

			"NumpadDecimal" = .NP_Decimal,
			"NumpadDivide" = .NP_Divide,
			"NumpadMultiply" = .NP_Multiply,
			"NumpadSubtract" = .NP_Subtract,
			"NumpadAdd" = .NP_Add,
			"NumpadEnter" = .NP_Enter,
		}
	}

	res := s.key_from_js_event_key_code[e.key.code]

	if res == .None {
		log.errorf("Unhandled key code: %v", e.key.code)
	}

	return res
}
